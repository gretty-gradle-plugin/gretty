plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

repositories {
    maven {
        url "file:${project.property("privateRepoDir")}"
    }
    mavenCentral()
}

group = rootProject.group
version = rootProject.version

configurations {
    providedCompile
    api.extendsFrom providedCompile
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

compileGroovy {
    groovyOptions.configurationScript = file("${rootProject.projectDir}/gradle/config.groovy")
}

task assertJDKVersion {
    outputs.upToDateWhen { false }

    doFirst {
        if (JavaVersion.current() != project.targetCompatibility) {
            throw new GradleException('This build must be run with java ' + project.targetCompatibility)
        }
    }
}

tasks.named('publish').configure {
   dependsOn assertJDKVersion
}

ext.configurePublications = { String publicationName ->
    String capitalizedPublicationName = publicationName.capitalize()

    def thisProject = project

    publishing {
        publications.named(publicationName, MavenPublication) {
            pom {
                name = thisProject.name
                packaging = 'jar'
                description = thisProject.description
                url = thisProject.project_website

                scm {
                    url = thisProject.project_scm
                    connection = thisProject.project_scm
                    developerConnection = thisProject.project_scm
                }

                licenses {
                    license {
                        name = thisProject.license
                        url = thisProject.license_url
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = thisProject.developerId
                        name = thisProject.developerName
                    }
                }
            }
        }
    }

    project.task('publishToPrivateRepository') {
        dependsOn "publish${capitalizedPublicationName}PublicationToPrivateRepository"
    }
    project.tasks.build.dependsOn project.tasks.publishToPrivateRepository
    rootProject.tasks.testAllIntegrationTests.dependsOn project.tasks.publishToPrivateRepository
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named("javadocJar", Jar).configure {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    def groovydocTask = tasks.groovydoc
    dependsOn groovydocTask
    from groovydocTask.destinationDir
}

afterEvaluate {
    dependencies {
        testImplementation libs.spock
    }

    publishing {
        repositories {
            maven {
                name 'private'
                url "file:/${project.rootProject.ext.privateRepoDir}"
            }
        }
    }
} // afterEvaluate

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    publishing {
        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = project.name
                    packaging = 'jar'
                    // optionally artifactId can be defined here
                    description = project.description
                    url = project.project_website

                    scm {
                        connection = project.project_scm
                        developerConnection = project.project_scm
                        url = project.project_scm
                    }

                    licenses {
                        license {
                            name = project.license
                            url = project.license_url
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = project.developerId
                            name = project.developerName
                        }
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}
