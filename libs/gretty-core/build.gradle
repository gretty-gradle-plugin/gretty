plugins {
  id 'grettybuild.library'
}

import org.apache.tools.ant.filters.*

dependencies {
  // #231 If we decide to drop Gradle 6 support / Groovy 2 support, we can drop choosing Groovy versions at runtime again.
  compileOnly localGroovy()
  compileOnly "org.slf4j:slf4j-api:$slf4j_version"

  api "commons-cli:commons-cli:$commons_cli_version"
  api 'commons-configuration:commons-configuration:1.10'
  api "commons-io:commons-io:$commons_io_version"
  api 'org.apache.commons:commons-lang3:3.3.2'
  api 'org.bouncycastle:bcprov-jdk15on:1.60'
  api project(':libs:gretty-common')
}

tasks.processResources {
  def props = [
    grettyVersion: project.version,
    jetty11Version: project.jetty11_version,
    jetty11ServletApiVersion: project.jetty11_servlet_api_version,
    tomcat10Version: project.tomcat10_version,
    tomcat10ServletApiVersion: project.tomcat10_servlet_api_version,
    springBootVersion: project.springBootVersion,
    springLoadedVersion: project.springLoadedVersion,
    springVersion: project.springVersion,
    asmVersion: project.asm_version
  ]
  inputs.properties props
  filter ReplaceTokens, tokens: props
}
